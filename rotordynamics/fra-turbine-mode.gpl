G = 9810. ## mm/sec2
m = 12. ##kg, turbine mass
Jp = 270000. ## kg.mm2, turbine polar MI
Jd = Jp /2. ## kg.mm2, turbine diametral MI
b =  800. ##mm, distance to turbine cg
L = 1000. ##mm, length of the rotor

k = 2000. ## kgf/mm, support stiffness
d =   0.4  ## kgf.s/mm, support damping

unb = 1000. ## g.mm unbalance on the turbine
unb = 0.001 * unb / G ## length * kgs / G
maxspeed = 4000. ## rpm, max speed
maxfreq = maxspeed * pi / 30.

A = Jd + m * b**2
C = Jp

A = A / G
C = C / G

i = {0., 1.}

f0 = 1./(2.*pi) * sqrt(k*L**2/A)
speed0 = f0 * 60.

F(w) = (-w**2*A + i*w*d*L**2 + w**2*C + k*L**2)**(-1) * i * b*w**2*unb

uy(w) =  L * imag(F(w))
uz(w) = -L * real(F(w))

maxspeed = 1.2 * speed0
set xrange [0.: maxspeed]
set xlabel "Speed, rpm"
set grid

set title sprintf("Frequency Response\n Natural Frequency, rpm = %.2f", speed0)
set ylabel "Deflection, mm"
plot uy(x*pi/30.) with lines title "Y direction",\
     uz(x*pi/30.) with lines title "Z diredction"
	 