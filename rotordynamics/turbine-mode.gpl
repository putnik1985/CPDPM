G = 9810. ## mm/sec2
m = 12. ##kg, turbine mass
Jp = 270000. ## kg.mm2, turbine polar MI
Jd = Jp /2. ## kg.mm2, turbine diametral MI
b =  800. ##mm, distance to turbine cg
L = 1000. ##mm, length of the rotor

k = 2000. ## kgf/mm, support stiffness
d =   0.4  ## kgf.s/mm, support damping

maxspeed = 4000. ## rpm, max speed
maxfreq = maxspeed * pi / 30.

A = Jd + m * b**2
C = Jp

A = A / G
C = C / G

re(w) = k*L**2/A + w*w*C*C/(4.*A**2) - d**2*L**4/(4.*A**2)
im(w) = 2.*w*C*d*L**2/(4.*A**2)

i = {0., 1.}
z(w) = re(w) + i * im(w)

r(w) = abs(z(w))
phi(w) = arg(z(w))

l1(w) = sqrt(r(w)) * exp(i * phi(w)/2.)
l2(w) = sqrt(r(w)) * exp(i * phi(w)/2. + i * pi)

p1(w) = w*C/(2.*A) + i * d * L**2 / (2.*A) + l1(w)
p2(w) = w*C/(2.*A) + i * d * L**2 / (2.*A) + l2(w)

f1(w) = abs(real(p1(w))) / (2.*pi)
f2(w) = abs(real(p2(w))) / (2.*pi)

log1(w) = abs(imag(p1(w))) / f1(w)
log2(w) = abs(imag(p2(w))) / f2(w)

e(speed) = maxfreq * speed / maxspeed

set xrange [0.:maxspeed]
set xlabel "Speed, rpm"
set grid

set multiplot layout 2,1

set title sprintf("Whirl Map\n")
set ylabel "Freq, Hz"
plot f1(x*pi/30.) with lines title "Mode#1",\
     f2(x*pi/30.) with lines title "Mode#2",\
	 e(x) with lines title "1E"

set title sprintf("Stability Map\n")
set ylabel "LogDec"
plot log1(x*pi/30.) with lines title "Mode#1",\
     log2(x*pi/30.) with lines title "Mode#2"
	 
unset multiplot	 