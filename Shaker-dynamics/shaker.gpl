fs = 3. ## shaker frequency, Hz
fp = 120. ## part frequency, Hz

Ms = 50.  ## shaker mass, kg
Mp = 100. ## part mass, kg

c = 2000. ## stiffness of the rod between shaker and part, kgs/mm
Fs = 20.  ## force generated in the shaker, kgs

G = 9810. ## mm/sec2

Ms = Ms / G
Mp = Mp / G

H(f) = (1./c) * (((4.*pi**2*fs**2-4.*pi**2*f**2)*Ms/c + 1.) * ((4.*pi**2*fp**2-4.*pi**2*f**2)*Mp/c + 1.) - 1.)**(-1.)

xp(f) = H(f) * Fs
xs(f) = (1. + (4.*pi**2*fp**2 - 4.*pi**2*f**2)*Mp/c)*H(f)*Fs

Fp(f) = c * (xp(f) - xs(f))

set title sprintf("Shaker Response \n Ms = %.4f, Mp = %.4f \n fs = %.2f, fp = %.2f \n", Ms, Mp, fs, fp)
set xrange [0.:2.*fp]
plot abs(xp(x)) with lines,\
     abs(xs(x)) with lines
      
