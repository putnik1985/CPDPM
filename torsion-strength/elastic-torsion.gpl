a = 1.
b = 2.


beta(n) = pi * n / a
B(n) = (a/(pi*n))**2 * (4./(pi*n)) * ((-1)**n - 1.)

N = 54 ## can not take more, 
w(x,y) = sum[n=1:N] B(n)/sinh(beta(n)*b) * ((1. - cosh(beta(n)*b)) * sinh(beta(n)*y) - (1. - cosh(beta(n)*y)) * sinh(beta(n)*b)) * sin(pi*n*x/a) 
dwx(x,y) = sum[n=1:N] B(n)/sinh(beta(n)*b) * ((1. - cosh(beta(n)*b)) * sinh(beta(n)*y) - (1. - cosh(beta(n)*y)) * sinh(beta(n)*b)) * cos(pi*n*x/a) * (pi*n/a)
dwy(x,y) = sum[n=1:N] B(n)/sinh(beta(n)*b) * beta(n) * ( cosh(beta(n)*y) - cosh(beta(n)*b)*cosh(beta(n)*y) + sinh(beta(n)*y) * sinh(beta(n)*b) ) * sin(pi*n*x/a)

f(x) = sum[n=1:N] (4./(pi*n)) * ((-1)**n - 1.) * sin(pi*n*x/a)

unset surface
unset key
####set key
set contour
set view map
set samples 25, 25
set isosamples 26, 26
set cntrlabel start 2 interval 12 font ",7"
set cntrparam order 8
set cntrparam levels 8
set cntrparam bspline 

set xrange [0:a]
set yrange [0:b]
set xlabel "X"
set ylabel "Y"
set grid

set multiplot layout 2,2

set title "w(x,y)"
splot w(x,y),\
      w(x,y) with labels
	  
set title "dwx(x,y)"
splot dwx(x,y),\
      dwx(x,y) with labels
	  
set title "dwy(x,y)"
splot dwy(x,y),\
      dwy(x,y) with labels
  
unset multiplot