a = 1.
b = 2.

set xrange [-a:a]
set yrange [-b:b]

l(n) = 2.*b*sinh(pi*n*a/b)
m(n) = 2.*a*sinh(pi*n*b/a)


A(n) = 2. / l(n) * (16. * b**3 / (pi*n)**3 - 2. * b**3 / (pi*n)) *  (1. - (-1)**n)
B(n) = 2. / l(n) * (16. * b**3 / (pi*n)**3 - 2. * b**3 / (pi*n)) *  (1. - (-1)**n)
C(n) = 2. / m(n) * b*b * (1. - (-1)**n) * 2.*a/(pi*n)
D(n) = 2. / m(n) * b*b * (1. - (-1)**n) * 2.*a/(pi*n)

N = 100
v1(x1,y1) = sum[n=1:N] A(n) * sinh(pi*n/(2*b) * (2*a - x1)) * sin(pi*n/(2.*b)*y1)
v2(x1,y1) = sum[n=1:N] B(n) * sinh(pi*n/(2*b) * (x1)) *  sin(pi*n/(2.*b)*y1)
v3(x1,y1) = sum[n=1:N] C(n) *  sin(pi*n/(2*a) * (x1)) * sinh(pi*n/(2.*a)*(2.*b - y1))
v4(x1,y1) = sum[n=1:N] D(n) *  sin(pi*n/(2*a) * (x1)) * sinh(pi*n/(2.*a)*y1)

## solution
v(x1,y1) = v1(x1,y1) + v2(x1,y1) + v3(x1,y1) + v4(x1,y1)

###unset surface
###unset key
set contour
###set view map
set samples 25, 25
set isosamples 26, 26
set cntrlabel start 2 interval 20 font ",7"
set cntrparam order 8
set cntrparam levels 10
set cntrparam bspline 
set xlabel "X"
set ylabel "Y"

splot v(x + a,y + b) - y**2 title "surface"
